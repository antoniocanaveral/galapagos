/**
* FUNCION SII.F_CGG_VEH_VEHICULO_CHECK
* VERIFICA SI EL VEHICULO ESTA REGULARIZADO PARA INICIAR UN NUEVO TRAMITE. ESTA FUNCION ES CONSUMIDA DESDE
* LA FUNCION DE SELECCION PAGINADO SII.F_CGG_VEH_VEHICULO_SELECT@F_CGG_VEH_VEHICULO.SQL.
* @param IN_CVVEH_CODIGO IDENTIFICATIVO UNICO DE VEHICULO
* @return BOOLEAN
*/
CREATE OR REPLACE FUNCTION SII.F_CGG_VEH_VEHICULO_CHECK(
IN IN_CVVEH_CODIGO SII.CGG_VEH_VEHICULO.CVVEH_CODIGO%TYPE
)RETURNS BOOLEAN AS
$$
DECLARE
OUT_CHECK BOOLEAN;
TMP_COUNT INT;
BEGIN
	SELECT COUNT(VH.CVVEH_CODIGO) INTO TMP_COUNT 
	FROM SII.CGG_VEH_VEHICULO VH
	INNER JOIN SII.CGG_RES_TRAMITE TRM ON TRM.CVVEH_CODIGO = VH.CVVEH_CODIGO AND TRM.CRTRA_FECHA_RECEPCION = (SELECT MAX(CRTRA_FECHA_RECEPCION) FROM SII.CGG_RES_TRAMITE WHERE CVVEH_CODIGO = VH.CVVEH_CODIGO)
	INNER JOIN SII.CGG_RES_SEGUIMIENTO SGM ON SGM.CRTRA_CODIGO = TRM.CRTRA_CODIGO AND SGM.CRSEG_ESTADO_ATENCION = 4 AND SGM.CRSEG_TIPO_RESPUESTA = 1 AND SGM.CRSEG_FECHA_DESPACHO = (SELECT MAX(CRSEG_FECHA_DESPACHO) FROM SII.CGG_RES_SEGUIMIENTO WHERE CRTRA_CODIGO = TRM.CRTRA_CODIGO)
	WHERE CVVEH_INGRESO AND VH.CVVEH_CODIGO = IN_CVVEH_CODIGO;
	RETURN TMP_COUNT > 0;
END
$$
LANGUAGE 'plpgsql' VOLATILE CALLED ON NULL INPUT;

/**
* FUNCION SII.F_CGG_VEH_VEHICULO_CHECK_PERSONA
* VERIFICA SI EL SOLICITANTE CUMPLE TODOS LOS REQUISITOS MINIMOS PARA INICIAR UN TRAMITE DE VEHICULOS.
* @param IN_CRPER_CODIGO CODIGO DE REGISTRO DE LA PERSONA SOLICITANTE
* @param IN_CRTST_CODIGO CODIGO DEL TIPO DE SOLICITUD
* @return OBSERVACIONES
*/
CREATE OR REPLACE FUNCTION SII.F_CGG_VEH_VEHICULO_CHECK_PERSONA(
IN IN_CRPER_CODIGO VARCHAR,
IN IN_CRTST_CODIGO VARCHAR
)RETURNS VARCHAR AS
$$
DECLARE
OUT_MSG VARCHAR;
TMP_RECORD RECORD;
TMP_SW BOOLEAN;
--TIPO DE SOLICITUD DE TRAMITE PARA INGRESO DE VEHICULOS
TMP_TRAMITE_INGRESO VARCHAR;
--TIPO DE SOLICITUD DE TRAMITE PARA SALIDA DE VEHICULOS
TMP_TRAMITE_SALIDA VARCHAR;
--TIPO DE SOLICITUD DE TRAMITE PARA REEMPLAZO DE VEHICULOS
TMP_TRAMITE_REEMPLAZO VARCHAR;
BEGIN
	--SELECCION DE VALORES DE CONFIGURACION DEL SISTEMA
	SELECT CGCNF_VALOR_CADENA INTO TMP_TRAMITE_INGRESO FROM SII.CGG_CONFIGURACION WHERE CGCNF_CODIGO = 'CONF21';
	SELECT CGCNF_VALOR_CADENA INTO TMP_TRAMITE_SALIDA FROM SII.CGG_CONFIGURACION WHERE CGCNF_CODIGO = 'CONF22';
	SELECT CGCNF_VALOR_CADENA INTO TMP_TRAMITE_REEMPLAZO FROM SII.CGG_CONFIGURACION WHERE CGCNF_CODIGO = 'CONF23';
	
	TMP_SW := TRUE;
	OUT_MSG := '[';
	
	--VERIFICA SI EL SOLICITANTE ES RESIDENTE PERMANENTE
	FOR TMP_RECORD IN (SELECT PRS.CRPER_CODIGO FROM SII.CGG_RES_PERSONA PRS 
		INNER JOIN SII.CGG_RES_RESIDENCIA RSD ON RSD.CRPER_CODIGO = PRS.CRPER_CODIGO AND RSD.CRRSD_MODALIDAD = 0 AND RSD.CRRSD_VIGENTE
		WHERE PRS.CRPER_CODIGO = IN_CRPER_CODIGO) LOOP
		TMP_SW := FALSE;
	END LOOP;
	IF (TMP_SW) THEN
		OUT_MSG := OUT_MSG||QUOTE_LITERAL('El Residente solicitante no tiene residencia permanente.');
	END IF;
	
	--VERIFICA SI LA PERSONA TIENE OBSERVACIONES PENDIENTES QUE NO HAN SIDO ATENDIDAS
	FOR TMP_RECORD IN (SELECT CROBS_DESCRIPCION 
		FROM SII.CGG_RES_OBSERVADO 
		WHERE CRPER_CODIGO = IN_CRPER_CODIGO AND NOT CROBS_RESUELTO) LOOP
		IF (LENGTH(OUT_MSG) > 1) THEN
			OUT_MSG := OUT_MSG||',';
		END IF;
		OUT_MSG := OUT_MSG||QUOTE_LITERAL(TMP_RECORD.CROBS_DESCRIPCION);
	END LOOP;
	
	--VERIFICA SI LA PERSONA TIENE CUPO DISPONIBLE VEHICULAR PARA INGRESAR UN VEHICULO
	FOR TMP_RECORD IN (SELECT CRPER_CUPO_VEHICULAR FROM SII.CGG_RES_PERSONA WHERE CRPER_CODIGO = IN_CRPER_CODIGO AND NOT CRPER_CUPO_VEHICULAR AND TMP_TRAMITE_INGRESO = IN_CRTST_CODIGO) LOOP
		IF (LENGTH(OUT_MSG) > 1) THEN
			OUT_MSG := OUT_MSG||',';
		END IF;
		OUT_MSG := OUT_MSG||QUOTE_LITERAL('El Residente solicitante no dispone de cupo vehicular.');
	END LOOP;
	
	--VERIFICA SI LA PERSONA TIENE CUPO VEHICULAR OCUPADO PARA SACAR O REEMPLAZAR UN VEHICULO
	FOR TMP_RECORD IN (SELECT CRPER_CUPO_VEHICULAR FROM SII.CGG_RES_PERSONA WHERE CRPER_CODIGO = IN_CRPER_CODIGO AND CRPER_CUPO_VEHICULAR AND IN_CRTST_CODIGO IN (TMP_TRAMITE_SALIDA, TMP_TRAMITE_REEMPLAZO)) LOOP
		IF (LENGTH(OUT_MSG) > 1) THEN
			OUT_MSG := OUT_MSG||',';
		END IF;
		OUT_MSG := OUT_MSG||QUOTE_LITERAL('El Residente solicitante tiene el cupo vehicular disponible.');
	END LOOP;
	
	OUT_MSG := OUT_MSG||']';
	RETURN OUT_MSG;
END
$$
LANGUAGE 'plpgsql' VOLATILE CALLED ON NULL INPUT;

